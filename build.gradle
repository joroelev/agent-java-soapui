buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.3'
        classpath 'net.researchgate:gradle-release:2.6.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

description = 'EPAM Report portal. SoapUI client API'


allprojects {


    apply plugin: 'java'

    group = 'com.epam.reportportal'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'net.researchgate.release'
    apply plugin: 'com.jfrog.bintray'


    apply from: "${rootProject.projectDir}/quality.gradle"
    apply from: "${rootProject.projectDir}/release.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8


    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "http://dl.bintray.com/epam/reportportal" }
        maven { url "http://www.soapui.org/repository/maven2" }
    }

    dependencies {
        compileOnly 'log4j:log4j:1.2.17'
        compile 'com.github.reportportal:client-java:591b7b41'
        compile 'com.github.reportportal:commons-model:feec39f3'
        compile 'com.epam.reportportal:logger-java-log4j:5.0.0-BETA-8'
    }


    //noinspection GroovyAssignabilityCheck
    task zip(type: Zip, overwrite: true) {
        def path = "${project.projectDir}/src/main/resources/"
        classifier = 'full'
        from shadowJar.outputs.files
        from(path, {
            include 'soapui-log4j.xml'
            into('/bin')
        })
        from(path, {
            include 'reportportal-listeners.xml'
            into('/bin/listeners')
        })
    }
    build.dependsOn zip
}

//noinspection GroovyAssignabilityCheck
task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}